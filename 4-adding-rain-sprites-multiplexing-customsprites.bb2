; Inspired from @Earok work (creator of the Scorpion Engine)
; https://github.com/earok/BlitzBasicDemos/blob/master/SpriteMultiplex/main.bb 
; Required to have BB2OBJTYPES.RES in resident libs
; Fourth part - Adding rain using CustomSprites from BlitzBasic 2
; Copper Rainbow Background - not possible (or complicated) - 
; "Note that these two commands may NOT be mixed with the commands above"

WBStartup
DEFTYPE .w

#RandomArraySize=64

Dim RainRandom.b(#RandomArraySize)
EVEN
For r.b=0 to #RandomArraySize-1
  RainRandom(r)=Rnd(20) 
Next

#DEBUGCOLOR=0

BitMap 0,320,256,3
BitMap 1,640,256,3
BitMap 2,320,256,2

InitPalette 0,32
LoadBitMap 0,"img/bg.iff",0
;LoadPalette 0, "img/bg.iff", 0
LoadBitMap 1,"img/fg.iff",1
LoadPalette 0, "img/pal.iff", 0

LoadBitMap 2,"img/rain4.iff"
LoadPalette 0, "img/rain.iff", 16
Use BitMap 2
GetaShape 0,0,0,16,16
GetaSprite 0,0
GetaShape 1,16,0,16,16
GetaSprite 1,1

InitCopList 0,44,256,$00004+$00020+$00010,8,32,4*7+2;

Blitz
CreateDisplay 0
DisplayPalette 0,0
DisplayBitMap 0,1,0
VWait

; Copper Rainbow Background - ALso not possible to do a copper rainbow background
;CopperReset 0,0
;CopperWait 0,0
;CopperMove $180,$00f
;CopperWait 0,40
;CopperMove $180,$00d
;CopperWait 0,80
;CopperMove $180,$00e
;CopperWait 0,120
;CopperMove $180,$00c
;CopperWait 0,160
;CopperMove $180,$00b
;CopperWait 0,200
;CopperMove $180,$00a
;CopperWait 0,240
;CopperMove $180,$009
;CopperWait 0,255

CustomSprites 0, 0, 120, 7


VWait
If #DEBUGCOLOR
    Use BitMap 0
    For i=0 To 31
        Boxf i lsl 3,0,(i lsl 3)+7,7,i
    Next
    Use BitMap 1
    For i=0 To 31
        Boxf i lsl 3,10,(i lsl 3)+7,17,i
    Next
EndIf

BlitzKeys On
SX.w=0
INCGLOBAL.b=2
R.b=0
While RawStatus($45)=0
    VWait	
    If Joyx(1)=1
        SX + 1
    EndIf
    If SX>=640
        SX=0
    End If

    INCGLOBAL + 1
	IF INCGLOBAL =3
		X=0
		Y=0
		ID=0
		COUNT=0
		CHANNEL=0
		While CHANNEL<15
			X=RainRandom(R) LSL 4
			IF COUNT = 2
				Y+32
				COUNT=0
			Endif
			COUNT + 1
			IF R=64
				R=0
			EndIf
			R + 1
			DisplaySprite 0,ID,X,Y,CHANNEL
			CHANNEL + 1
			If CHANNEL = 7
				CHANNEL + 1 ; SKIP last channnel because smooth scrolling is activated.
				COUNT + 1
			EndIf
			ID + 1
			if ID = 2
				ID=0
			endif
		Wend
		INCGLOBAL=0
	EndIf

    DisplayBitMap 0,1,SX,0,0,0,0
Wend

End
